#Область ПрограммныйИнтерфейс

// Возвращает значение показателей АПК выбранных из документов Документ.ОтчетЗагруженныхФорм
//
// Параметры:
// Период - СправочникСсылка.Периоды - Период АПК,
// Организация - СправочникСсылка.Организации - элемент справочника Организация,
// ВидДеятельности - Строка(3) - Пока используется только "СХ",
// ФайлФормы - Строка(30) - Фильтр по полю ФайлФормы, Если Неопределено - то не используется,
// КодПоказателя - Строка (100) - Фильтр по полю КодПоказателя, Если Неопределено - то не используется,
// ЗначениеПоУмолчанию - Число - Если значение не найдено то возвращает значение переданное в данном параметре,
//							Если неопределено - не устанавливается для ненайденного ключа.
// Возвращаемое значение:
//   Структура - Имя Показателя, Значение Показателя. Формируется динамически,
//
Функция ПолучитьПоказателиАПК(Период, Организация, ВидДеятельности, ФайлФормы = Неопределено, КодПоказателя = Неопределено, ЗначениеПоУмолчанию = Неопределено) Экспорт
	
	Показатели = Новый Структура;
	Запрос = Новый Запрос;
	Запрос.Текст = "ВЫБРАТЬ РАЗЛИЧНЫЕ
	               |	ОтчетЗагруженныхФормПараметрыОтчета.КодПоказателя КАК КодПоказателя,
	               |	ОтчетЗагруженныхФормПараметрыОтчета.Значение КАК Значение,
	               |	ОтчетЗагруженныхФормПараметрыОтчета.Ссылка.ФайлФормы КАК ФайлФормы
	               |ИЗ
	               |	Документ.ОтчетЗагруженныхФорм.ПараметрыОтчета КАК ОтчетЗагруженныхФормПараметрыОтчета
	               |		ВНУТРЕННЕЕ СОЕДИНЕНИЕ (ВЫБРАТЬ
	               |			СогласованийСвязьФорм.СписокФорм КАК СписокФорм
	               |		ИЗ
	               |			Справочник.Согласований.СвязьФорм КАК СогласованийСвязьФорм
	               |		ГДЕ
	               |			СогласованийСвязьФорм.Ссылка.Период = &Период
	               |			И СогласованийСвязьФорм.Ссылка.ВидДеятельности = &ВидДеятельности) КАК ВложенныйЗапрос
	               |		ПО ОтчетЗагруженныхФормПараметрыОтчета.Ссылка.ФайлФормы = ВложенныйЗапрос.СписокФорм
	               |ГДЕ
	               |	ОтчетЗагруженныхФормПараметрыОтчета.Ссылка.Период = &Период
	               |	И ОтчетЗагруженныхФормПараметрыОтчета.Ссылка.Организация = &Организация
	               |	И ОтчетЗагруженныхФормПараметрыОтчета.Ссылка.ВидДеятельности = &ВидДеятельности
	               |	И (ОтчетЗагруженныхФормПараметрыОтчета.Ссылка.ФайлФормы = &ФайлФормы
	               |			ИЛИ &ФайлФормы = НЕОПРЕДЕЛЕНО)
	               |	И (ОтчетЗагруженныхФормПараметрыОтчета.КодПоказателя = &КодПоказателя
	               |			ИЛИ &КодПоказателя = НЕОПРЕДЕЛЕНО)
				   |";

	Запрос.УстановитьПараметр("Период",          Период);
	Запрос.УстановитьПараметр("Организация",     Организация);
	Запрос.УстановитьПараметр("ВидДеятельности", ВидДеятельности);
	Запрос.УстановитьПараметр("ФайлФормы",       ФайлФормы);
	Запрос.УстановитьПараметр("КодПоказателя",   КодПоказателя);

	РезультатЗапроса = Запрос.Выполнить().Выбрать();
	Пока РезультатЗапроса.Следующий() Цикл
		Показатели.Вставить("" + СокрЛП(РезультатЗапроса.ФайлФормы) + "_" + СокрЛП(РезультатЗапроса.КодПоказателя), РезультатЗапроса.Значение);
	КонецЦикла;
	
	Если ФайлФормы <> Неопределено 
			И КодПоказателя <> Неопределено 
			И ЗначениеПоУмолчанию <> Неопределено Тогда
		ИмяСвойства = "" + СокрЛП(ФайлФормы) + "_" + СокрЛП(КодПоказателя);
		Если Не Показатели.Свойство(ИмяСвойства) Тогда
			Показатели.Вставить(ИмяСвойства, ЗначениеПоУмолчанию);
		КонецЕсли;	
	КонецЕсли;
	
	Возврат Показатели;

КонецФункции

#КонецОбласти
