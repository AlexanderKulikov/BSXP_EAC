
&НаКлиенте
Процедура КопироватьПоля(Команда)
	Закрыть(ПолучитьСтруктуруКопирования());
КонецПроцедуры

&НаСервере
Функция ПолучитьСтруктуруКопирования()
	
	Исключения = РеквизитыИсключения();
	
	СтруктураКопирования = Новый Структура;
	Для Каждого Реквизит Из Справочники.ЕАС_ПоляФормШаблонов.ПустаяСсылка().Метаданные().Реквизиты Цикл
		Если Реквизит.Имя = "ДатаНачала" 
				И ЭтаФорма.УстановитьПериод Тогда
			 СтруктураКопирования.Вставить("ДатаНачала", 	ЭтаФорма.Период.ДатаНачала);
			 СтруктураКопирования.Вставить("ДатаОкончания", ЭтаФорма.Период.ДатаОкончания);
		 ИначеЕсли Исключения.Найти(Реквизит.Имя) = Неопределено 
			 	И ЭтаФорма["Установить"+Реквизит.Имя] Тогда
			 СтруктураКопирования.Вставить(Реквизит.Имя, ЭтаФорма[Реквизит.Имя]);
		КонецЕсли;	 
	КонецЦикла;	
	
	Возврат СтруктураКопирования; 
КонецФункции

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	// Добавление Реквизитов Формы
	Исключения = РеквизитыИсключения();
	
    ДобавляемыеРеквизиты = Новый Массив;
		
	Для Каждого Реквизит Из Справочники.ЕАС_ПоляФормШаблонов.ПустаяСсылка().Метаданные().Реквизиты Цикл
		Если Исключения.Найти(Реквизит.Имя) = Неопределено Тогда
			РеквизитФормы = Новый РеквизитФормы("Установить"+Реквизит.Имя, Новый ОписаниеТипов("Булево"), ,Реквизит.Синоним);
			ДобавляемыеРеквизиты.Добавить(РеквизитФормы);
			РеквизитФормы = Новый РеквизитФормы(Реквизит.Имя, Реквизит.Тип, ,Реквизит.Синоним);
			ДобавляемыеРеквизиты.Добавить(РеквизитФормы);
		ИначеЕсли Реквизит.Имя = "ДатаНачала" Тогда
			ДобавляемыеТипы = Новый Массив;
			ДобавляемыеТипы.Добавить(Тип("СтандартныйПериод"));
			ОписаниеПериода = Новый ОписаниеТипов(ДобавляемыеТипы);  
			РеквизитФормы = Новый РеквизитФормы("УстановитьПериод", Новый ОписаниеТипов("Булево"), , "Установить период");
			ДобавляемыеРеквизиты.Добавить(РеквизитФормы);
			РеквизитФормы = Новый РеквизитФормы("Период", ОписаниеПериода, , "Период");
			ДобавляемыеРеквизиты.Добавить(РеквизитФормы);
		КонецЕсли;	
	КонецЦикла;	
	
    ИзменитьРеквизиты(ДобавляемыеРеквизиты);
	
    // Добавление Элементов Формы
	
	Для Каждого Реквизит Из Справочники.ЕАС_ПоляФормШаблонов.ПустаяСсылка().Метаданные().Реквизиты Цикл
		Если Исключения.Найти(Реквизит.Имя) = Неопределено Тогда
			
		    НоваяГруппа = Элементы.Добавить("Группа" + Реквизит.Имя, Тип("ГруппаФормы"), Элементы.ГруппаРеквизиты);
			НоваяГруппа.Вид         = ВидГруппыФормы.ОбычнаяГруппа;
			НоваяГруппа.Группировка = ГруппировкаПодчиненныхЭлементовФормы.ГоризонтальнаяВсегда;
			НоваяГруппа.Отображение = ОтображениеОбычнойГруппы.Нет;
			
		    НовыйЭлемент = Элементы.Добавить(Реквизит.Имя, Тип("ПолеФормы"), НоваяГруппа);
			НовыйЭлемент.ПутьКДанным = Реквизит.Имя;
			НовыйЭлемент.Вид 		 = ВидПоляФормы.ПолеВвода;
			
		    НовыйЭлемент = Элементы.Добавить("Установить"+Реквизит.Имя, Тип("ПолеФормы"), НоваяГруппа);
			НовыйЭлемент.ПутьКДанным 		= "Установить"+Реквизит.Имя;
			НовыйЭлемент.Вид 				= ВидПоляФормы.ПолеФлажка;
			НовыйЭлемент.Подсказка 			= "Установить " + Реквизит.Синоним;
			НовыйЭлемент.ПоложениеЗаголовка = ПоложениеЗаголовкаЭлементаФормы.Нет;
		ИначеЕсли Реквизит.Имя = "ДатаНачала" Тогда
		    НоваяГруппа = Элементы.Добавить("ГруппаПериод", Тип("ГруппаФормы"), Элементы.ГруппаРеквизиты);
			НоваяГруппа.Вид         = ВидГруппыФормы.ОбычнаяГруппа;
			НоваяГруппа.Группировка = ГруппировкаПодчиненныхЭлементовФормы.ГоризонтальнаяВсегда;
			НоваяГруппа.Отображение = ОтображениеОбычнойГруппы.Нет;
			
		    НовыйЭлемент = Элементы.Добавить(Реквизит.Имя, Тип("ПолеФормы"), НоваяГруппа);
			НовыйЭлемент.ПутьКДанным = "Период";
			НовыйЭлемент.Вид 		 = ВидПоляФормы.ПолеВвода;
			
			НовыйЭлемент = Элементы.Добавить("УстановитьПериод", Тип("ПолеФормы"), НоваяГруппа);
			НовыйЭлемент.ПутьКДанным 		= "УстановитьПериод";
			НовыйЭлемент.Вид 				= ВидПоляФормы.ПолеФлажка;
			НовыйЭлемент.Подсказка 			= "Установить период";
			НовыйЭлемент.ПоложениеЗаголовка = ПоложениеЗаголовкаЭлементаФормы.Нет;
		КонецЕсли;	
	КонецЦикла;	
	
КонецПроцедуры

Функция РеквизитыИсключения()
	Исключения = Новый Массив;
	Исключения.Добавить("ОбластьДанных");
	Исключения.Добавить("ДатаНачала");
	Исключения.Добавить("ДатаОкончания");
	Возврат Исключения;
КонецФункции
